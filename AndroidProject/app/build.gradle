plugins {
    id 'com.android.application'
    id 'com.google.dagger.hilt.android'
    id "org.sonarqube" version "4.0.0.2929"
    id 'jacoco'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

apply plugin: "jacoco"

jacoco {
    toolVersion = '0.8.10'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*'] // needed to support roboelectric with jacoco
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {

    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def mainSrc = "$project.projectDir/src/main/java"
    def javaTree = fileTree(dir: "$project.buildDir/intermediates/javac/debug/classes", excludes: fileFilter)

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([javaTree]))
    executionData.setFrom(fileTree(dir: project.buildDir, includes: ['jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec']))
}

sonar {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "isuc"
        property "sonar.login", "120537998e2c122476f30cade8d4a25865210fa6"
        property "sonar.projectKey", "App-CarChargers-Grupo4"
        property "sonar.projectName", "App-CarChargers-Grupo4"

        // I need this property to avoid the error where sonarqube does not close some files and
        // prevents a clean afterwards
        property "sonar.scm.disabled", true

        // this property is deprecated, now I use the xml file defined below it
        // property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/testDebugUnitTest.exec"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
    }
}

android {
    namespace 'es.unican.carchargers'
    compileSdk 33

    defaultConfig {
        applicationId "es.unican.carchargers"
        minSdk 28
        targetSdk 33
        versionCode 1
        versionName "1.0"

//        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner "es.unican.carchargers.utils.CustomTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Mockito
    def mockito_version= '5.5.0'
    testImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"

    // Retrofit (for REST API access) https://square.github.io/retrofit/
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Idling Resource: needed in Espresso to wait for Retrofit async calls
    implementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    implementation 'com.android.support.test.espresso.idling:idling-concurrent:3.5.0'

    // to automate Parcelable implementation
    implementation 'org.parceler:parceler-api:1.1.12'
    annotationProcessor 'org.parceler:parceler:1.1.12'

    // Hilt Dependency Injection
    implementation 'com.google.dagger:hilt-android:2.44.2'
    annotationProcessor 'com.google.dagger:hilt-compiler:2.44.2'
    // For instrumentation tests
    androidTestImplementation  'com.google.dagger:hilt-android-testing:2.44.2'
    androidTestAnnotationProcessor 'com.google.dagger:hilt-compiler:2.44.2'
    // For local unit tests
    testImplementation 'com.google.dagger:hilt-android-testing:2.44.2'
    testAnnotationProcessor 'com.google.dagger:hilt-compiler:2.44.2'
}